// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        buildConfiguration = [
                compileVersion     : 29,
                minSdkVersion      : 17,
                targetSdkVersion   : 29,
                sourceCompatibility: JavaVersion.VERSION_1_8,
                targetCompatibility: JavaVersion.VERSION_1_8,
        ]

        releaseConfiguration = [
                releaseVersion    : "3.1.0-SNAPSHOT",
                releaseVersionCode: 30100,
        ]

        libraries = [
                android    : [
                        'appcompat': 'com.android.support:appcompat-v7:28.0.0',
                        'fragment' : 'com.android.support:support-fragment:28.0.0',
                ],

                'androidx' : [
                        'appcompat'   : 'androidx.appcompat:appcompat:1.2.0',
                        'fragment'    : 'androidx.fragment:fragment:1.0.0',
                        'recyclerview': 'androidx.recyclerview:recyclerview:1.2.0',
                ],

                'growingio': [
                        'tracker_core'    : "com.growingio.android:tracker-core:$releaseConfiguration.releaseVersion",
                        'autotracker_core': "com.growingio.android:autotracker-core:$releaseConfiguration.releaseVersion",
                        'tracker_core_cdp': "com.growingio.android:tracker-core-cdp:$releaseConfiguration.releaseVersion",
                        'tracker_cdp'     : "com.growingio.android:tracker-cdp:$releaseConfiguration.releaseVersion",
                        'autotracker_cdp' : "com.growingio.android:autotracker-cdp:$releaseConfiguration.releaseVersion",
                ],
        ]
    }

    repositories {
        mavenLocal()
        mavenCentral()
        google()
        jcenter()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:3.5.0"
        classpath "com.novoda:bintray-release:0.9.2"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.18.0"
        classpath "com.growingio.android:autotracker-gradle-plugin:${releaseConfiguration.releaseVersion}"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        mavenLocal()
        google()
        jcenter()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects { project ->
    apply plugin: 'checkstyle'

    task checkstyle(type: Checkstyle, group: 'check') {
        configFile rootProject.file('checkstyle/checkstyle.xml')
        configProperties.checkstyleJavaHeader = rootProject.file('checkstyle/java.header')
        source 'src/main/java', 'src/test/java', 'src/androidTest/java'
        ignoreFailures false
        showViolations true
        include '**/*.java'
        classpath = files()
    }

    afterEvaluate {
        if (project.tasks.findByName('check')) {
            check.dependsOn('checkstyle')
        }
    }
}